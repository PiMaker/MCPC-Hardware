/*
Provides helper functions regarding the integrated VGA framebuffer.
*/

#ifndef LIB_VGA
#define LIB_VGA

#include "base.mscr"

// Starting address of VGA framebuffer CFG region
#define VGA_BASE_ADDR 0xE000

// The newline character
#define VGA_NEWLINE 0xA

// VGA dimensions
#define VGA_DIM_X 98
#define VGA_DIM_Y 60

// Globals responsible for maintaining internal position buffering
global vga_buf_pos_x = 0;
global vga_buf_pos_y = 0;

// Prints a single character and advances the buffer position by one
func void vga_printChar(char) {
    // Handle newlines
    if char == VGA_NEWLINE {
        vga_buf_pos_x = 0;

        if (vga_buf_pos_y + 1) == VGA_DIM_Y {
            vga_shiftConsoleDown(true);
        } else {
            vga_buf_pos_y += 1;
        }

        return 0;
    }

    // Set framebuffer at buffer location to given character
    $$(
        VGA_BASE_ADDR + (vga_buf_pos_y * VGA_DIM_X) + vga_buf_pos_x,
        char
    );

    // Advance buffer
    vga_buf_pos_x += 1;

    if vga_buf_pos_x == VGA_DIM_X {
        vga_buf_pos_x = 0;

        if (vga_buf_pos_y + 1) == VGA_DIM_Y {
            vga_shiftConsoleDown(true);
        } else {
            vga_buf_pos_y += 1;
        }
    }
}

// Prints an entire null-terminated string
func void vga_printString(str) {
    var i = 0;
    var charAt = $(str);
    while charAt != 0 {
        vga_printChar(charAt);
        i += 1;
        charAt = $(str + i);
    }
}

func void vga_shiftConsoleDown(clearBottomRow) {
    // TODO
}

#endif
